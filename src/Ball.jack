class Ball {
	field int posX, posY, speedX, speedY;

	constructor Ball new(int startX, int startY, int velocityX, int velocityY) {
		let posX = startX;
        let posY = startY;
        let speedX = velocityX;
        let speedY = velocityY;
		do draw();
		return this;
	}

	// Освобожднеие памяти
	method void dispose() {
		 do Memory.deAlloc(this);
		 return;
	}
	
	// Стираем мяч
	method void erase() {
      do Screen.setColor(false);
      do Screen.drawCircle(posX, posY, 3);
      return;
   }

	// Рисуем мяч
	method void draw() {
		 do Screen.setColor(true);
		 do Screen.drawCircle(posX, posY, 3);
		 return;
	}

	// Движение мяча
	method void move() {
		do erase();

		if ((posX < 5) | (posX > 506)) { let speedX = -speedX; }
        if (posY < 5) { let speedY = -speedY; }

		let posX = posX + speedX;
        let posY = posY + speedY;

		do draw();

		return;
	}

	//геттеры
	method int getX() { return posX; }
    method int getY() { return posY; }
	method int getSpeedY() { return speedY; }
	method int getSpeedX() { return speedX; }

	// сеттер скорости по Y
	method void setSpeedY(int speed) { 
		let speedY = speed;
		return;
	}

	// сеттер скорости по X
	method void setSpeedX(int speed) { 
		let speedX = speed;
		return;
	}
}
